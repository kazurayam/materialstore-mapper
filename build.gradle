plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'com.kazurayam'
version '0.1.1'

ext.isReleaseVersion = ! version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    groovyVersion = '2.4.20'
    slf4jVersion = '1.7.31'
    commonsioVersion = '2.8.0'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }
tasks.withType(GroovyCompile).each {it.groovyOptions.encoding = defaultEncoding }

dependencies {
    implementation "org.codehaus.groovy:groovy-all:${groovyVersion}"
    implementation group: 'com.kazurayam', name: 'materialstore', version: '0.4.5-SNAPSHOT'


    // https://mvnrepository.com/artifact/org.apache.poi/poi
    //implementation group: 'org.apache.poi', name: 'poi', version: '5.2.0'
    implementation group: 'org.apache.poi', name: 'poi', version: '3.17'

    // https://mvnrepository.com/artifact/org.apache.poi/poi-ooxml
    //implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.2.0'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '3.17'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-csv
    implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.9.0'

    // https://mvnrepository.com/artifact/net.sf.cssbox/pdf2dom
    implementation group: 'net.sf.cssbox', name: 'pdf2dom', version: '2.0.1'

    // https://mvnrepository.com/artifact/org.apache.pdfbox/pdfbox
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.25'

    // https://mvnrepository.com/artifact/org.apache.pdfbox/pdfbox-tools
    implementation group: 'org.apache.pdfbox', name: 'pdfbox-tools', version: '2.0.25'

    implementation group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: "${slf4jVersion}"
    // https://mvnrepository.com/artifact/commons-io/commons-io
    testImplementation group: 'commons-io', name: 'commons-io', version: "${commonsioVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.1'

}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        materialstore(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
            pom {
                groupId = project.group
                name = project.rootProject.name
                description = 'classes that implements com.kazurayam.materialstore.map.Mapper interface, which includes Excel->CSV conversion, PDF->Image conversion'
                url = 'https://kazurayam.github.io/materialstore-mapper/'
                from components.java
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org.licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kazurayam'
                        name = 'URAYAMA,kazuaki'
                        email = 'kazuaki.urayama@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/kazurayam/${project.rootProject.name}.git"
                    developerConnection = "scm:git:git@github.com:kazurayam/${project.rootProject.name}.git"
                    url = "https://github.com/kazurayam/${project.rootProject.name}"
                }
            }
        }
    }
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo: snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
}

signing {
    sign publishing.publications.materialstore
}
// I do not want to sign the SNAPSHOT
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}


task copyJavadocToDocsDir(type: Copy) {
    from 'build/docs/javadoc'
    into 'docs/api'
}
javadoc.finalizedBy copyJavadocToDocsDir